plugins {
    id 'com.android.application'
}

apply from: "../config.gradle"//需要引入使用，app gradle才可以调用

android {

    compileSdkVersion AndroidVersion.compileSdkVersion
    buildToolsVersion AndroidVersion.buildToolsVersion

    compileOptions {
        sourceCompatibility AndroidOptions.sourceCompatibility
        targetCompatibility AndroidOptions.targetCompatibility
    }


    //省略前面的代码..
    repositories {
        flatDir {
            dirs '../common/libs/', 'libs'
        }
    }

    defaultConfig {

        applicationId AppId.app

        //版本名称
        versionName AndroidConfig.versionName

        //版本号
        versionCode AndroidConfig.versionCode

        //最低安装版本
        minSdkVersion AndroidConfig.minSdkVersion

        //目标适配版本
        targetSdkVersion AndroidConfig.targetSdkVersion

        //国际化语言
        resConfigs AndroidConfig.resConfigs

        //混淆文件
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        //应用平台
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }

        //路由框架
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    //APK版本
    buildTypes {
        debug {
            //暴露给Java工程代码，生成Config.Build类
            buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
        }
        release {}
    }

}

dependencies {
    //外部库导入
    Main.each { k, v -> implementation v }
    //注解处理器导入
    AnnotationProcessors.each { k, v -> annotationProcessor v}
    //公共模块导入
    implementation project(path: ':common')
    //weight模块导入
    implementation project(path: ':lib_weight')
    //manager模块导入
    implementation project(path: ':lib_manager')
    /**正式版本下，各个模块**/
    if (isRelease) {
        //派健康子APP
        implementation project(path: ':app_health')
        //派健康子APP下的module_login
        implementation project(path: ':module_login')
        //Me模块
        implementation project(path: ':module_me')
        //Home模块
        implementation project(path: ':module_home')
        //Message模块
        implementation project(path: ':module_message')
        //Money模块
        implementation project(path: ':module_money')
    }
}